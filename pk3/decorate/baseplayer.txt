ACTOR TacDMPlayer: PlayerPawn
{
    Speed 1
    Health 100
    Radius 16
    Height 56
    Mass 100
    PainChance 255
    Player.DisplayName "Walking Target"
    Player.CrouchSprite "PLYC"
    Player.ColorRange 112, 127

    States
    {
      Spawn:
        PLAY A -1
        Loop

      See:
        PLAY ABCD 4
        Loop

      Missile:
        PLAY E 12
        Goto Spawn

      Melee:
        PLAY F 6 BRIGHT
        Goto Missile

      Pain:
        PLAY G 4
        PLAY G 4 A_Pain
        Goto Spawn

      Death:
        PLAY H 0 ACS_ExecuteAlways(755, 0, 0,0,0)
        PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")

      Death1:
        PLAY H 10
        PLAY I 10 A_PlayerScream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N -1
        Stop

      XDeath:
        PLAY H 0 ACS_ExecuteAlways(755, 0, 0,0,0)
        PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")

      XDeath1:
        PLAY O 5
        PLAY P 5 A_XScream
        PLAY Q 5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        Stop

      AltSkinDeath:
        PLAY H 6
        PLAY I 6 A_PlayerScream
        PLAY JK 6
        PLAY L 6 A_NoBlocking
        PLAY MNO 6
        PLAY P -1
        Stop

      AltSkinXDeath:
        PLAY Q 5 A_PlayerScream
        PLAY R 0 A_NoBlocking
        PLAY R 5 A_SkullPop
        PLAY STUVWX 5
        PLAY Y -1
        Stop
    }
}

actor IsBot: Inventory
{
    +UNDROPPABLE
    Inventory.MaxAmount 1
}




actor SpawnStop: CustomInventory
{
    States
    {
      Pickup:
        TNT1 A 0 SetPlayerProperty(0, 1, 0)
        stop
    }
}

actor SpawnProtection: CustomInventory
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("SOLID", 0)
        TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
        TNT1 A 0 A_ChangeFlag("NOTARGET", 1)
        TNT1 A 0 A_HideThing
        stop
    }
}

actor EndSpawnProtection: CustomInventory
{
    States
    {
      Pickup:
        TNT1 A 0 SetPlayerProperty(0, 0, 0)
        TNT1 A 0 A_ChangeFlag("SOLID", 1)
        TNT1 A 0 A_ChangeFlag("SHOOTABLE", 1)
        TNT1 A 0 A_ChangeFlag("NOTARGET", 1)
        TNT1 A 0 A_UnhideThing
        stop
    }
}

actor ClassSwitcher: CustomInventory   // only given in Coop
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
    +INVBAR

    Inventory.Icon "PINVB0"

    States
    {
      Spawn:
        TNT1 A -1
        stop

      Use:
        TNT1 A 0 ACS_ExecuteAlways(733)
        fail
    }
}

actor ClassSwitcherNoInvuln: CustomInventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
    +INVBAR

    Inventory.Icon "PINVB0"

    States
    {
      Spawn:
        TNT1 A -1
        stop

      Use:
        TNT1 A 0 ACS_ExecuteAlways(733, 0, 1)
        fail
    }
}
