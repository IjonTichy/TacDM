actor "Semi-Shotgun": TacDMWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 8
    Weapon.AmmoGive2 2
    Weapon.AmmoType1 "Buckshot"
    Weapon.AmmoType2 "Slug"

    Weapon.SlotNumber 3

    DamageType TacDMPhysical
    Weapon.PreferredSkin "T-Shotgun"

    Inventory.PickupMessage "Semi-automatic shotgun"
    Obituary "%k resisted %o."
    +NOAUTOFIRE
    +NOALERT

    States
    {
      Deselect:
        PKSG A 1 A_Lower
        Loop

      Select:
        PKSG A 1 A_Raise
        Loop

      Ready:
        PKSG A 1 A_WeaponReady
        Loop

      Fire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(5, 3, 10, 5, "TacDMBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 7 bright A_PlaySound("tacticaldm/shotfire", CHAN_WEAPON)
        goto GenericPump

      AltFire:  // Weaker, more accurate slug
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(1, 1, -1, 30, "TacDMBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 0 A_PlaySound("tacticaldm/shotfire", CHAN_WEAPON)
        PKSG A 2 bright offset(0, 36)
        PKSG A 4 bright offset(0, 37)
        PKSG A 2 bright offset(0, 36)
        PKSG A 2 bright offset(0, 34)
        goto GenericPump

      GenericPump:
        PKSG BCD 1
        PKSG E 2
        PKSG F 5 A_PlaySound("weapons/sshoto", 5)
        PKSG E 3
        PKSG D 2
        PKSG CB 1
        PKSG B 0 A_ReFire
        Goto Ready

      Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone

      AltFlash:
        SHTF A 2 Bright A_Light1
        SHTF B 7 Bright A_Light2
        Goto LightDone

      Spawn:
        SHOT A -1
        Stop
    }
}
