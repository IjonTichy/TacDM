actor "Scattergun": TacDMWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 6
    Weapon.AmmoGive2 0
    Weapon.AmmoType1 "Buckshot"
    Weapon.AmmoType2 "Slug"

    Weapon.SlotNumber 3

    DamageType TacDMPhysical
    Weapon.PreferredSkin "T-Shotgun"

    Inventory.PickupMessage "Weaker pump-action shotgun"
    Obituary "%k scattered %o across the room."
    +NOAUTOFIRE
    +NOALERT

    States
    {
      Ready:
        PKSG A 1 A_WeaponReady
        Loop
      Deselect:
        PKSG A 1 A_Lower
        Loop
      Select:
        PKSG A 1 A_Raise
        Loop

      Fire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(7, 7, 15, 3, "TacDMBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 7 A_PlaySound("tacticaldm/shotfire", CHAN_WEAPON)
        goto GenericPump

      AltFire: // Weaker slug (not accurate for the scattergun, but still damaging)
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(2, 2, -1, 30, "TacDMBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 11 A_PlaySound("tacticaldm/shotfire", CHAN_WEAPON)
        goto GenericPump

      GenericPump:
        PKSG B 2
        PKSG CD 1
        PKSG E 10 A_PlaySound("weapons/sshoto", 5)
        PKSG DC 1
        PKSG B 1
        PKSG B 0 A_ReFire
        Goto Ready

      Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone

      AltFlash:
        SHTF A 2 Bright A_Light1
        SHTF B 7 Bright A_Light2
        Goto LightDone
      Spawn:
        SHOT A -1
        Stop
    }
}
