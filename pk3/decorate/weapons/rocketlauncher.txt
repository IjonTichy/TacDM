actor "Rocket Launcher": TacDMWeapon
{
    Weapon.SelectionOrder 800

    Weapon.AmmoType1 "TacDMRocketAmmo"
    Weapon.AmmoType2 "TacDMRocketAmmo"
    Weapon.AmmoUse1  1
    Weapon.AmmoUse2  5
    Weapon.AmmoGive1 2

    Weapon.SlotNumber 4
    Weapon.PreferredSkin "T-Rocket Launcher"

    +NOAUTOFIRE

    Inventory.PickupMessage "Rocket launcher"

    States
    {
      Select:
        PKRL A 1 A_Raise
        loop

      Deselect:
        PKRL A 1 A_Lower
        loop

      Ready:
        PKRL A 1 A_WeaponReady
        loop

      Fire:
        PKRL D 0 A_PlaySound("weapons/rocklf", CHAN_WEAPON)
        PKRL A 0 A_FireCustomMissile("TacDMRocket")
        PKRL A 1 bright A_GunFlash
        PKRL B 2 bright
        PKRL D 8 bright
        PKRL C 6
        PKRL B 4
        PKRL E 3
        PKRL F 2
        PKRL F 1
        PKRL A 4
        PKRL A 0 A_ReFire
        goto Ready

      AltFire:
        PKRL A 3 A_GunFlash
        PKRL B 1
        PKRL D 0 A_PlaySound("tacticaldm/rocketaltfire", CHAN_WEAPON)
        PKRL D 0 A_Recoil(6)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket", -15, 0, -6, 0, 0,  0)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",  15, 0,  6, 0, 0,  0)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",  -5, 0, -3, 0, 2,  3)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",   5, 0,  3, 0, 2,  3)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",  -5, 0, -3, 0, 2, -3)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",   5, 0,  3, 0, 2, -3)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",   2, 0, -6, 0, 0,  0)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",  -2, 0,  6, 0, 0,  0)
        PKRL D 0  A_FireCustomMissile("TacDMTinyRocket",   0, 0,  0, 0, 0,  2)
        PKRL D 18 A_FireCustomMissile("TacDMTinyRocket",   0, 1,  0, 0, 0, -2)
        PKRL C 6
        PKRL B 4
        PKRL E 2
        PKRL FG 1
        PKRL A 0 A_ReFire
        goto Ready

      Flash:
        PKRF A 1 Bright A_Light1
        PKRF B 2 Bright
        PKRF C 1 Bright A_Light2
        PKRF DE 3 Bright
        goto LightDone

      AltFlash:
        PKRF A 3 Bright A_Light1
        PKRF B 1 Bright
        PKRF C 2 Bright A_Light2
        PKRF DE 3 Bright
        goto LightDone

      Spawn:
        LAUN A -1
        stop
    }
}

actor TacDMRocketBase
{
    Radius 4
    Height 4
    Speed 50

    Projectile
    Damage 0

    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL

    Decal Scorch
    DamageType TacDMExplosion

    Obituary "%k applied %o as a primer."

    States
    {
      Spawn:
        MISL A 1 bright
        loop
    }
}



actor TacDMRocket: TacDMRocketBase
{
    Damage (80)

    States
    {
      Death:
        MISL B 0 bright A_Explode(224, 224)
        MISL B 0 A_SpawnItemEx("TacDMExplosion224")
        stop
    }
}

actor TacDMTinyRocket: TacDMRocketBase
{
    Damage (10)

    Scale 0.5
    Decal SmallScorch

    States
    {
      Death:
        MISL B 0 bright A_Explode(96, 96)
        MISL B 0 A_SpawnItemEx("TacDMExplosion96")
        stop
    }
}
