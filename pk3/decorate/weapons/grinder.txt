actor Grinder: TacDMWeapon
{
    Weapon.SelectionOrder 500

    Weapon.AmmoType "AmmoBelt"
    Weapon.AmmoGive 100
    Weapon.AmmoUse  1
    +ALT_AMMO_OPTIONAL

    Weapon.SlotNumber 3
    Inventory.PickupMessage "Grinder"

    AttackSound "weapons/minigun"

    Weapon.PreferredSkin "T-Minigun"
    Obituary "%k butchered %o."

    States
    {
      Deselect:
        MNGG A 0 A_JumpIfInventory("GrinderSpin", 1, "SpinDeselect")
        goto Deselect0

      SpinDeselect:
        MNGG A 0 ACS_ExecuteAlways(738, 0, 1, 0)
        MNGG A 0 A_StopSoundEx("SoundSlot5")
        MNGG A 0 A_PlaySoundEx("tacticaldm/grinderdown", "SoundSlot5", 0)
        MNGG A 0 A_TakeInventory("GrinderSpin")
        MNGG ACABCDAABBCCDDAAABBBCCCDDDAAAABBBBCCCCDDDD 1 A_Lower
        goto Deselect0

      Deselect0:
        MNGG A 0 A_StopSoundEx("SoundSlot5")
        MNGG A 1 A_Lower
        loop

      Select:
        MNGG A 0 A_TakeInventory("GrinderSpin")
        MNGG A 1 A_Raise
        loop

      Ready:
        MNGG A 0 A_JumpIfInventory("GrinderSpin", 1, "Spin")
        MNGG A 0 ACS_ExecuteAlways(738, 0, 1, 0)
        goto Ready0

      Ready0:
        MNGG A 1 A_WeaponReady
        loop

      Spin:
        MNGG A 0 A_PlaySoundEx("tacticaldm/grinderspin", "SoundSlot5", 1)
        MNGG A 0 ACS_ExecuteAlways(738, 0, 0, 50)
        MNGG A 0 A_AlertMonsters
        MNGG AC 1 A_WeaponReady
        loop

      Fire:
        MNGG A 0 A_JumpIfInventory("GrinderSpin", 1, "SpinFire")
        MNGG A 0 A_GunFlash
        MNGG A 0 ACS_ExecuteAlways(738, 0, 0, 80)
        MNGG A 1 bright A_FireBullets(3, 3, -1, 20, "TacDMBulletPuff")
        MNGG BCD 1 bright
        goto Hold

      Hold:
        MNGG A 0 A_JumpIfInventory("GrinderSpin", 1, "SpinFire")
        MNGG A 0 ACS_ExecuteAlways(738, 0, 0, 60)
        MNGG A 0 A_GunFlash
        MNGG A 1 bright A_FireBullets(3, 3, -1, 20, "TacDMBulletPuff")
        MNGG C 1 bright
        MNGG ABCD 1 bright A_ReFire
        MNGG A 0 ACS_ExecuteAlways(738, 0, 1, 0)
        MNGG ABCDAABBCCDD 2
        Goto Ready

      SpinFire:
        MNGG A 0 ACS_ExecuteAlways(738, 0, 0, 15)
        MNGG A 0 A_GunFlash
        MNGG A 1 bright A_FireBullets(5, 5, -1, 20, "TacDMBulletPuff")
        MNGG C 1 bright A_FireBullets(5, 5, -1, 20, "TacDMBulletPuff")
        MNGG C 0 A_Refire
        goto Ready

      Flash:
        MNGF A 1 BRIGHT A_Light1
        MNGF B 1 BRIGHT A_Light0
        Stop

      AltFire:
        MNGG A 0 A_JumpIfInventory("GrinderSpin", 1, "AltDown")
        MNGG A 0 A_PlaySoundEx("tacticaldm/grinderup", "SoundSlot5", 0)
        MNGG ABCD 4
        MNGG ABCD 3
        MNGG ABCD 2
        MNGG ABCD 1
        MNGG AC 1
        MNGG A 0 A_GiveInventory("GrinderSpin")
        goto Ready

      AltDown:
        MNGG A 0 ACS_ExecuteAlways(738, 0, 1, 0)
        MNGG A 0 A_StopSoundEx("SoundSlot5")
        MNGG A 0 A_PlaySoundEx("tacticaldm/grinderdown", "SoundSlot5", 0)
        MNGG AC 1
        MNGG ABCD 1
        MNGG ABCD 2
        MNGG ABCD 3
        MNGG ABCD 4
        MNGG A 0 A_TakeInventory("GrinderSpin")
        goto Ready

      Spawn:
        MNGN A -1
        Loop
    }
}


actor GrinderSpin: Inventory
{
    inventory.maxamount 1
    +UNDROPPABLE
}
