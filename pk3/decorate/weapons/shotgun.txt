actor "Pump-Action Shotgun": TacDMWeapon replaces Shotgun
{
    Weapon.SelectionOrder 1300
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 8
    Weapon.AmmoGive2 2
    Weapon.AmmoType1 "Buckshot"
    Weapon.AmmoType2 "Slug"

    Weapon.SlotNumber 3

    DamageType TacDMPhysical
    Weapon.PreferredSkin "T-Shotgun"

    Inventory.PickupMessage "Pump-action shotgun"
    Obituary "%k took down %o like a deer."
    +NOAUTOFIRE
    +NOALERT

    States
    {
      Ready:
        PKSG A 1 A_WeaponReady
        Loop
      Deselect:
        PKSG A 1 A_Lower
        Loop
      Select:
        PKSG A 1 A_Raise
        Loop

      Fire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(5, 3, 10, 5, "TacDMBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 7 A_PlaySound("tacticaldm/shotfire", CHAN_WEAPON)
        goto GenericPump

        AltFire: // Weaker, but more accurate, slug
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(1, 1, 1, 30, "TacDMBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 11 A_PlaySound("tacticaldm/shotfire", CHAN_WEAPON)
        goto GenericPump

      GenericPump:
        PKSG B 3
        PKSG C 2
        PKSG DE 1
        PKSG F 3
        PKSG A 0 A_PlaySound("tacticaldm/shotreload", CHAN_WEAPON)
        PKSG F 2
        PKSG G 1
        PKSG H 3
        PKSG G 1
        PKSG F 3
        PKSG E 2
        PKSG DC 1
        PKSG B 3
        PKSG B 0 A_ReFire
        Goto Ready

      Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone

      AltFlash:
        SHTF A 2 Bright A_Light1
        SHTF B 7 Bright A_Light2
        Goto LightDone
      Spawn:
        SHOT A -1
        Stop
    }
}
